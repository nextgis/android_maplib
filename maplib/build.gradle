/*
 * Project: NextGIS Mobile SDK
 * Author:  Dmitry Baryshnikov, dmitry.baryshnikov@nextgis.com
 *
 * Created by Dmitry Baryshnikov on 14.08.18 20:59.
 * Copyright (c) 2018-2019 NextGIS, info@nextgis.com.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Lesser Public License for more details.
 *
 * You should have received a copy of the GNU Lesser Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
//apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'kotlin-kapt'
apply plugin: "org.jetbrains.kotlin.plugin.parcelize"

def androidAbis = ["armeabi-v7a", "arm64-v8a", "x86_64", "x86"] //
def gdalFiles = [ "cubewerx_extra.wkt", "ecw_cs.wkt", "ellipsoid.csv", "epsg.wkt", "esri_StatePlane_extra.wkt", "gdalvrt.xsd", "gml_registry.xml", "gmlasconf.xml", "gmlasconf.xsd", "gt_datum.csv", "gt_ellips.csv", "ogrvrt.xsd", "stateplane.csv" ]
def sslFiles = ["cert.pem"]
def projFiles = ["proj.db"]
def buildConfig = android.defaultPublishConfig.toString()
def buildConfigUpper = buildConfig.substring(0, 1).toUpperCase() +
        buildConfig.substring(1, buildConfig.length())

ext {
    bintrayRepo = 'MapLib'
    bintrayName = 'com.nextgis.maplib'

    publishedGroupId = 'com.nextgis.maplib'
    libraryName = 'maplib'
    artifact = 'maplib'

    libraryDescription = 'The base map library for Android geo applications.'

    siteUrl = 'https://github.com/nextgis/android_maplib'
    gitUrl = 'https://github.com/nextgis/android_maplib.git'

    libraryVersion = '3.7.0'

    developerId = 'bishopgis'
    developerName = 'Dmitry Baryshnikov'
    developerEmail = 'dmitry.baryshnikov@nextgis.com'

    licenseName = 'GNU LESSER GENERAL PUBLIC LICENSE, Version 3.0'
    licenseUrl = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
    allLicenses = ["LGPL-3.0"]
}

//androidExtensions {
//    experimental = true
//}

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 20
        versionName '3.7.0'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters = androidAbis
        }

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti"
                arguments "-DSUPRESS_VERBOSE_OUTPUT=OFF",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_static",
                        "-DANDROID_CPP_FEATURES=rtti", //exceptions
//                        "-GAndroid Gradle - Unix Makefiles",
//                        "-DCMAKE_MAKE_PROGRAM=make",
                        "-DBUILD_SHARED_LIBS=ON",
                        "-DBUILD_STATIC_LIBS=OFF",
                        "-DBUILD_TARGET_PLATFORM=ANDROID"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("long", "VERSION_CODE1", "${defaultConfig.versionCode}")

        }
        debug {
            buildConfigField("long", "VERSION_CODE1", "${defaultConfig.versionCode}")
        }
    }
    externalNativeBuild {
        cmake {
            path "${projectDir}/src/main/ngstore/CMakeLists.txt"
        }
    }
    productFlavors {
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    dokka { // Needed for bintray
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/docs/javadoc"
//        sourceDirs = files('src/main/kotlin')
    }
    dataBinding {
        enabled = true
    }
    buildFeatures {
        viewBinding = true
    }
    ndkVersion '25.2.9519653'
    //buildToolsVersion '28.0.3'
}

task copyFiles() {
    description "Copy assets to ${projectDir}/src/main/assets"
    dependsOn tasks.findAll { task -> task.name.contains("externalNativeBuild${buildConfigUpper}") }

    gdalFiles.each { name ->
        copy {
            into "$projectDir/src/main/assets/gdal"
            from "$projectDir/.cxx/cmake/$buildConfig/x86_64/data/$name"
        }
    }

    projFiles.each { name ->
        copy {
            into "$projectDir/src/main/assets/proj"
            from "$projectDir/.cxx/cmake/$buildConfig/x86_64/data/$name"
        }
    }

    sslFiles.each { name ->
        copy {
            into "$projectDir/src/main/assets/certs"
            from "$projectDir/.cxx/cmake/$buildConfig/x86_64/data/$name"
        }
    }
}

// temporary disable cmake build
//tasks.matching { it.name.contains('externalNativeBuild') }.all {
//    it.enabled = false
//}

tasks.matching { it.name == 'externalNativeBuild' }.all {
    it.dependsOn(copyFiles)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "androidx.core:core-ktx:$core_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'io.sentry:sentry-android:1.7.23'
    implementation 'org.slf4j:slf4j-nop:1.8.0-beta4'
    implementation 'com.google.android.material:material:1.8.0'
}

// add it to the bottom of the file

//apply from: 'installv1.gradle'
//apply from: 'bintrayv1.gradle'
